<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">

    <title>NoFlo Development Environment</title>
    <link rel="shortcut icon" href="favicon.ico" />

    <!-- Polymer deps -->
    <script src="bower_components/platform/platform.js"></script>
    <link rel="import" href="bower_components/polymer/polymer.html">

    <!-- React -->
    <script src="bower_components/react/react.js"></script>

    <script src="browser/noflo-noflo-indexeddb/vendor/IndexedDBShim.min.js"></script>
    <link rel="import" href="bower_components/the-graph/the-graph-editor/the-graph-editor.html">
    <link rel="import" href="bower_components/the-graph/the-graph-thumb/the-graph-thumb.html">
    <noflo-polymer name="the-graph-editor" inports="graph library width height" outports="changed nodeclick edgeclick"></noflo-polymer>
    <link rel="import" href="browser/noflo-noflo-polymer/noflo-polymer/noflo-polymer.html">
    <link rel="import" href="elements/noflo-context.html">
    <noflo-polymer name="noflo-context" inports="project graphs component node edge search clear getpanel" outports="project graphs currentgraph component newgraph nodes edges result toolpanel contextpanel"></noflo-polymer>
    <link rel="import" href="elements/noflo-main.html">
    <noflo-polymer name="noflo-main" inports="open projects graphs components runtimes route token" outports="newproject project graph component download githubtoken runtime"></noflo-polymer>
    <link rel="import" href="elements/noflo-runtime.html">
    <noflo-polymer name="noflo-runtime" inports="graphs edges panel runtimes runtime" outports="runtime changed"></noflo-polymer>
    <link rel="import" href="elements/noflo-search.html">
    <noflo-polymer name="noflo-search" inports="toggle results project graphs component panel" outports="menu search"></noflo-polymer>
    <link rel="import" href="elements/noflo-library.html">
    <noflo-polymer name="noflo-library" inports="editor search" outports="result"></noflo-polymer>
    <link rel="import" href="elements/noflo-account.html">
    <link rel="import" href="elements/noflo-project.html">
    <noflo-polymer name="noflo-project" inports="project graphs components githubtoken graphs component" outports="newgraph newcomponent changed upload"></noflo-polymer>
    <link rel="import" href="elements/noflo-component-editor.html">
    <noflo-polymer name="noflo-component-editor" inports="component graphs width height" outports="changed"></noflo-polymer>
    <script src="./browser/noflo-ui.js"></script>
    <link rel="stylesheet" href="bower_components/font-awesome/css/font-awesome.min.css">
    <link rel="stylesheet" href="./css/noflo-ui.css">
    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0, user-scalable=no" />
  </head>
  <body>
    <div id="container">
      <noflo-main></noflo-main>
      <noflo-context></noflo-context>
      <noflo-runtime></noflo-runtime>
      <noflo-search></noflo-search>
      <noflo-library></noflo-library>
      <noflo-project></noflo-project>
      <main id="workspace">
        <noflo-component-editor></noflo-component-editor>
        <the-graph-editor></the-graph-editor>
      </main>
    </div>
    <script type="application/fbp" id="PrepareStorage">
      INPORT=Open.NAME:NAME
      OUTPORT=Errors.OUT:ERROR
      OUTPORT=Open.DB:DB

      # Open database connection
      Open(indexeddb/Open) ERROR -> IN Errors(core/Merge)

      # Specify storage version to get correct migrations
      '2' -> VERSION Open

      # Database upgrade setup
      Open UPGRADE -> UPGRADE Upgrade(indexeddb/UpgradeRouter)

      # ## Version 1

      # Project storage
      'projects' -> NAME CreateProjects(indexeddb/CreateStore)
      'id' -> KEYPATH CreateProjects
      Upgrade VERSIONS -> DB CreateProjects
      CreateProjects ERROR -> IN Errors

      # Graph storage
      'graphs' -> NAME CreateGraphs(indexeddb/CreateStore)
      'id' -> KEYPATH CreateGraphs
      CreateProjects DB -> DB CreateGraphs
      CreateGraphs ERROR -> IN Errors
      'project' -> NAME GraphProject(indexeddb/CreateIndex)
      'project' -> KEYPATH GraphProject
      CreateGraphs STORE -> STORE GraphProject
      # Migrate old LocalStorage data
      GraphProject STORE -> GRAPHSTORE Migrate(ui/MigrateLocalStorage)
      GraphProject ERROR -> IN Errors

      # Component storage
      'components' -> NAME CreateComponents(indexeddb/CreateStore)
      'id' -> KEYPATH CreateComponents
      CreateGraphs DB -> DB CreateComponents
      CreateComponents ERROR -> IN Errors
      'project' -> NAME ComponentProject(indexeddb/CreateIndex)
      'project' -> KEYPATH ComponentProject
      CreateComponents STORE -> STORE ComponentProject
      ComponentProject ERROR -> IN Errors

      # ## Version 2

      # Runtime storage
      'runtimes' -> NAME CreateRuntimes(indexeddb/CreateStore)
      'id' -> KEYPATH CreateRuntimes
      Upgrade VERSIONS -> DB CreateRuntimes
      CreateRuntimes ERROR -> IN Errors
    </script>
    <script type="application/fbp" id="LoadData">
      INPORT=Read.DB:DB
      INPORT=Start.IN:LOAD
      OUTPORT=GetProjects.ITEM:PROJECTS
      OUTPORT=ConvertGraph.OUT:GRAPHS
      OUTPORT=GetComponents.ITEM:COMPONENTS
      OUTPORT=GetRuntimes.ITEM:RUNTIMES
      OUTPORT=Read.COMPLETE:COMPLETE
      'readonly' -> DATA HoldMode(core/Kick) OUT -> MODE Read(indexeddb/BeginTransaction)
      Start(core/Repeat) OUT -> IN HoldMode
      'projects,graphs,components,runtimes' -> DATA HoldStores(core/Kick) OUT -> STORES Read
      Start OUT -> IN HoldStores

      # Read projects list
      'projects' -> DATA HoldProjectStore(core/Kick) OUT -> NAME ProjectStore(indexeddb/GetStore)
      Start OUT -> IN HoldProjectStore
      Read TRANSACTION -> TRANSACTION ProjectStore
      '' -> DATA HoldAllProjects(core/Kick) OUT -> ALL GetProjects(indexeddb/Query)
      Start OUT -> IN HoldAllProjects
      ProjectStore STORE -> STORE GetProjects

      # Read graphs list
      'graphs' -> DATA HoldGraphStore(core/Kick) OUT -> NAME GraphStore(indexeddb/GetStore)
      Start OUT -> IN HoldGraphStore
      ProjectStore TRANSACTION -> TRANSACTION GraphStore
      '' -> DATA HoldAllGraphs(core/Kick) OUT -> ALL GetGraphs(indexeddb/Query)
      GetProjects ITEM -> IN HoldAllGraphs
      GraphStore STORE -> STORE GetGraphs
      GetGraphs ITEM -> IN ConvertGraph(graph/LoadJson)

      # Read components list
      'components' -> DATA HoldComponentStore(core/Kick) OUT -> NAME ComponentStore(indexeddb/GetStore)
      Start OUT -> IN HoldComponentStore
      GraphStore TRANSACTION -> TRANSACTION ComponentStore
      '' -> DATA HoldAllComponents(core/Kick) OUT -> ALL GetComponents(indexeddb/Query)
      GetProjects ITEM -> IN HoldAllComponents
      ComponentStore STORE -> STORE GetComponents

      # Read runtimes list
      'runtimes' -> DATA HoldRuntimeStore(core/Kick) OUT -> NAME RuntimeStore(indexeddb/GetStore)
      Start OUT -> IN HoldRuntimeStore
      GraphStore TRANSACTION -> TRANSACTION RuntimeStore
      '' -> DATA HoldAllRuntimes(core/Kick) OUT -> ALL GetRuntimes(indexeddb/Query)
      Start OUT -> IN HoldAllRuntimes
      RuntimeStore STORE -> STORE GetRuntimes
    </script>
    <script type="application/fbp" id="StoreGraph">
      INPORT=Convert.IN:ITEM
      INPORT=HoldDb.DATA:DB
      OUTPORT=PutGraph.KEY:KEY
      'toJSON' -> METHOD Convert(objects/CallMethod)
      Convert OUT -> IN EnsureId(local/EnsureId) OUT -> IN Disc(core/DisconnectAfterPacket)
      Disc OUT -> IN SplitGraph(core/Split)
      SplitGraph OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      SplitGraph OUT -> IN HoldMode
      'graphs' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      SplitGraph OUT -> IN HoldStore
      SplitStore OUT -> NAME GraphStore(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION GraphStore STORE -> STORE PutGraph(indexeddb/Put)
      'project' -> KEY GetProject(objects/GetObjectKey)
      SplitGraph OUT -> IN GetProject OBJECT -> VALUE PutGraph
      GetProject OUT -> IN DropProject(core/Drop)
      '' -> DATA HoldProject(core/Kick) OUT -> VALUE SetProject(objects/SetPropertyValue)
      SplitGraph OUT -> IN HoldProject
      'project' -> PROPERTY SetProject
      GetProject MISSED -> IN SetProject OUT -> VALUE PutGraph
    </script>
    <script type="application/fbp" id="PrepareProject">
      INPORT=EnsureId.IN:IN
      INPORT=StoreGraph.DB:DB
      OUTPORT=SetMain.OUT:OUT
      EnsureId(local/EnsureId) OUT -> IN Split(core/Split)
      'id' -> KEY GetId(objects/GetObjectKey)
      'project' -> PROPERTY SetProject(objects/SetPropertyValue)
      GetId OUT -> VALUE SetProject
      Split OUT -> IN GetId
      'name' -> KEY GetName(objects/GetObjectKey)
      'name' -> PROPERTY SetName(objects/SetPropertyValue)
      GetName OUT -> VALUE SetName
      Split OUT -> IN GetName
      'type' -> KEY GetType(objects/GetObjectKey)
      'type' -> PROPERTY SetType(objects/SetPropertyValue)
      GetType OUT -> VALUE SetType
      Split OUT -> IN GetType
      Split OUT -> START InitializeGraph(objects/CreateObject)
      InitializeGraph OUT -> IN SetName OUT -> IN SetType OUT -> IN SetProject OUT -> DETAILS ToGraph(graph/CreateGraph)
      ToGraph OUT -> IN GenGraphId(local/EnsureId)
      GenGraphId OUT -> ITEM StoreGraph(local/StoreGraph)
      'main' -> PROPERTY SetMain(objects/SetPropertyValue)
      StoreGraph KEY -> IN SplitGraphId(core/Split) OUT -> VALUE SetMain
      Split OUT -> DATA HoldProject(core/Kick) OUT -> IN SetMain
      SplitGraphId OUT -> IN HoldProject
    </script>
    <script type="application/fbp" id="StoreProject">
      INPORT=Disc.IN:ITEM
      INPORT=HoldDb.DATA:DB
      OUTPORT=PutProject.KEY:KEY
      Disc(core/DisconnectAfterPacket) OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'projects' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE PutProject(indexeddb/Put)
      Split OUT -> VALUE PutProject
    </script>
    <script type="application/fbp" id="StoreComponent">
      INPORT=EnsureId.IN:ITEM
      INPORT=HoldDb.DATA:DB
      OUTPORT=Put.KEY:KEY
      EnsureId(local/EnsureId) OUT -> IN Disc(core/DisconnectAfterPacket)
      Disc OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'components' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE Put(indexeddb/Put)
      Split OUT -> VALUE Put
    </script>
    <script type="application/fbp" id="StoreRuntime">
      INPORT=Disc.IN:ITEM
      INPORT=HoldDb.DATA:DB
      OUTPORT=Put.KEY:KEY
      Disc(core/DisconnectAfterPacket) OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'runtimes' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE Put(indexeddb/Put)
      Split OUT -> VALUE Put
    </script>
    <script type="application/fbp" id="LoadExample">
      INPORT=SplitGist.IN:GIST
      OUTPORT=ParseExample.OUT:GRAPH
      OUTPORT=Errors.OUT:ERROR
      'https://api.github.com/gists' -> STRING GistPrefix(strings/SendString)
      '/' -> DELIMITER CreateUrl(strings/CompileString)
      SplitGist(core/Split) OUT -> IN GistPrefix
      GistPrefix OUT -> IN CreateUrl
      SplitGist OUT -> IN CreateUrl
      CreateUrl OUT -> URL GetGist(ajax/GetJsonP)
      'data' -> KEY GetData(objects/GetObjectKey)
      'files' -> KEY GetFiles(objects/GetObjectKey)
      'noflo.json' -> KEY GetFile(objects/GetObjectKey)
      'content' -> KEY GetFileContent(objects/GetObjectKey)
      GetGist OUT -> IN GetData
      GetData OUT -> IN GetFiles OUT -> IN GetFile OUT -> IN GetFileContent
      GetFiles MISSED -> IN Errors
      GetFileContent OUT -> IN ParseExample(strings/ParseJson)
    </script>
    <script type="application/fbp" id="EnsureId">
      INPORT=Item.IN:IN
      OUTPORT=SetId.OUT:OUT
      'id' -> PROPERTY SetId(objects/SetPropertyValue)
      Item(core/Repeat) OUT -> START Generate(ui/GenerateId)
      Generate OUT -> VALUE SetId
      Item OUT -> IN SetId
    </script>
    <script type="application/fbp" id="GithubUpload">
      INPORT=Repo.IN:REPO
      INPORT=Token.IN:TOKEN
      INPORT=Upload.PATH:NAME
      INPORT=Upload.MESSAGE:MESSAGE
      INPORT=WaitRepo.STRING:CONTENT

      # Plain repository name for repo creation
      '(.*)\/(.*)' -> PATTERN RepoName(strings/Replace)
      '$2' -> REPLACEMENT RepoName
      Repo(core/Repeat) OUT -> IN RepoName(strings/Replace)
      RepoName OUT -> DATA StartCreate(core/Kick)

      # Check if repository exists
      Token(core/Repeat) OUT -> TOKEN CheckRepo
      Repo OUT -> IN CheckRepo(github/GetRepository)
      CheckRepo ERROR -> IN Show(core/Output) OUT -> IN StartCreate
      # If not, we create it
      Token OUT -> TOKEN CreateRepo(github/CreateRepository)
      StartCreate OUT -> IN CreateRepo

      # Once we have repository we can upload the document
      Token OUT -> TOKEN Upload
      Repo OUT -> REPOSITORY Upload(github/SetContents)
      WaitRepo(strings/SendString) OUT -> IN Upload

      # Wait until the repository is ready
      CheckRepo OUT -> IN WaitRepo
      CreateRepo OUT -> IN WaitRepo

      Upload OUT -> IN Show(core/Output)
    </script>
    <script type="application/fbp" id="Upload">
      INPORT=GetToken.IN:IN
      'token' -> KEY GetToken(objects/GetObjectKey) OUT -> TOKEN Upload(local/GithubUpload)
      'repo' -> KEY GetRepo(objects/GetObjectKey) OUT -> REPO Upload
      'doc' -> KEY GetDoc(objects/GetObjectKey) OUT -> IN MakePath(ui/MakePath) OUT -> NAME Upload
      'message' -> KEY GetMessage(objects/GetObjectKey) OUT -> MESSAGE Upload
      'content' -> KEY GetContent(objects/GetObjectKey) OUT -> CONTENT Upload
      GetToken OBJECT -> IN GetRepo OBJECT -> IN GetDoc OBJECT -> IN GetMessage OBJECT -> IN GetContent
    </script>
    <script type="application/fbp" id="ProjectContext">
      INPORT=Context.PROJECT:PROJECT
      INPORT=Context.GRAPHS:GRAPHS
      INPORT=Context.COMPONENT:COMPONENT
      INPORT=Runtime.RUNTIMES:RUNTIMES
      INPORT=Context.CLEAR:CLEAR
      INPORT=Project.GITHUBTOKEN:GITHUBTOKEN
      INPORT=LoadProjectData.DB:DB
      OUTPORT=ListenGraph.OUT:GRAPHCHANGED
      OUTPORT=ComponentEditor.CHANGED:COMPONENTCHANGED
      OUTPORT=MergeNewGraph.OUT:NEWGRAPH
      OUTPORT=Project.NEWCOMPONENT:NEWCOMPONENT
      OUTPORT=Project.CHANGED:PROJECTCHANGED
      OUTPORT=Runtime.CHANGED:RUNTIMECHANGED
      OUTPORT=Project.UPLOAD:UPLOAD
      'noflo-context' -> SELECTOR Context(polymer/noflo-context)

      # Runtime handling
      Context GRAPHS -> GRAPHS Runtime(local/HandleRuntime)
      #Context EDGES -> EDGES Runtime

      # Search handling
      'noflo-search' -> SELECTOR Search(polymer/noflo-search)
      Context GRAPHS -> GRAPHS Search
      Context COMPONENT -> COMPONENT Search

      's' -> KEYS Shortcuts(interaction/ListenKeyboardShortcuts)
      Shortcuts SHORTCUT -> TOGGLE Search

      # Panel handling
      '' -> GETPANEL Context TOOLPANEL -> PANEL Runtime
      Context CONTEXTPANEL -> PANEL Search

      # Library handling
      'noflo-library' -> SELECTOR Library(polymer/noflo-library)

      # UI searching
      Search SEARCH -> SEARCH Library RESULT -> RESULTS Search
      Search SEARCH -> SEARCH Context RESULT -> RESULTS Search

      # Project menu
      'noflo-project' -> SELECTOR Project(polymer/noflo-project)
      Context PROJECT -> PROJECT Project
      Context GRAPHS -> GRAPHS Project
      Context COMPONENT -> COMPONENT Project
      Project NEWGRAPH -> IN MergeNewGraph(core/Merge)
      Context NEWGRAPH -> IN MergeNewGraph

      # Prepare graph editor
      Context CURRENTGRAPH -> CURRENTGRAPH GraphEditor(local/GraphEditor)
      Runtime RUNTIME -> RUNTIME GraphEditor
      Context NEWGRAPH -> NEWGRAPH GraphEditor
      Context PROJECT -> PROJECT GraphEditor
      GraphEditor NODECLICK -> NODE Context
      GraphEditor EDGECLICK -> EDGE Context
      GraphEditor EDITOR -> EDITOR Library

      # Prepare component editor
      'noflo-component-editor' -> SELECTOR ComponentEditor(polymer/noflo-component-editor)
      Context COMPONENT -> COMPONENT ComponentEditor
      Context GRAPHS -> GRAPHS ComponentEditor

      # Listen for graph changes
      Context CURRENTGRAPH -> IN ListenGraph(graph/ListenChanges)

      # Listen for screen size changes and communicate with editors
      '' -> START ListenWindow(interaction/ListenResize)
      ListenWindow WIDTH -> WIDTH GraphEditor
      ListenWindow HEIGHT -> HEIGHT GraphEditor
      ListenWindow WIDTH -> WIDTH ComponentEditor
      ListenWindow HEIGHT -> HEIGHT ComponentEditor
    </script>
    <script type="application/fbp" id="GraphEditor">
      INPORT=ConnectRuntime.RUNTIME:RUNTIME
      INPORT=ConnectRuntime.PROJECT:PROJECT
      INPORT=ConnectRuntime.NEWGRAPH:NEWGRAPH
      INPORT=Graph.IN:CURRENTGRAPH
      INPORT=GraphEditor.WIDTH:WIDTH
      INPORT=GraphEditor.HEIGHT:HEIGHT
      OUTPORT=Async.OUT:EDITOR
      OUTPORT=GraphEditor.NODECLICK:NODECLICK
      OUTPORT=GraphEditor.EDGECLICK:EDGECLICK
      'the-graph-editor' -> SELECTOR GraphEditor(polymer/the-graph-editor)
      Graph(core/Repeat) OUT -> START EmptyLibrary(objects/CreateObject)
      EmptyLibrary OUT -> LIBRARY GraphEditor
      Graph(core/Repeat) OUT -> GRAPH GraphEditor
      GraphEditor ELEMENT -> IN Async(core/RepeatAsync)
      Async OUT -> EDITOR ConnectRuntime(ui/ConnectRuntime)
    </script>
    <script type="application/fbp" id="HandleRuntime">
      INPORT=RuntimeElement.GRAPHS:GRAPHS
      INPORT=RuntimeElement.PANEL:PANEL
      INPORT=RuntimeElement.RUNTIMES:RUNTIMES
      OUTPORT=Connect.RUNTIME:RUNTIME
      OUTPORT=RuntimeElement.CHANGED:CHANGED

      'noflo-runtime' -> SELECTOR RuntimeElement(polymer/noflo-runtime)
      'body' -> SELECTOR GetParent(dom/GetElement) ELEMENT -> ELEMENT Connect(runtime/ConnectRuntime)
      RuntimeElement RUNTIME -> DEFINITION Connect
      Connect RUNTIME -> RUNTIME RuntimeElement
    </script>
    <script type="application/fbp" id="main">
      # Name of our IndexedDB database
      'noflo-ui' -> NAME Storage(local/PrepareStorage)

      # Prepare the main view
      'noflo-main' -> SELECTOR Main(polymer/noflo-main)

      # Pass database connection to persistence handlers
      Storage DB -> DB LoadData(local/LoadData)
      Storage DB -> DB StoreProject(local/StoreProject)
      Storage DB -> DB PrepareProject(local/PrepareProject)
      Storage DB -> DB StoreComponent(local/StoreComponent)
      Storage DB -> DB StoreGraph(local/StoreGraph)
      Storage DB -> DB StoreRuntime(local/StoreRuntime)
      Storage ERROR -> IN ShowErrors(core/Output)

      # Start by loading user's local data
      '' -> LOAD LoadData
      LoadData PROJECTS -> PROJECTS Main
      LoadData GRAPHS -> GRAPHS Main
      LoadData COMPONENTS -> COMPONENTS Main
      LoadData RUNTIMES -> RUNTIMES Main

      # Handle data changes from main
      Main NEWPROJECT -> IN PrepareProject OUT -> ITEM StoreProject
      Main RUNTIME -> ITEM StoreRuntime

      # Prepare the context
      Router(ui/Router) ROUTE -> CLEAR Context(local/ProjectContext)
      LoadData RUNTIMES -> RUNTIMES Context
      Router ROUTE -> ROUTE Main
      Main PROJECT -> PROJECT Context
      Main GRAPH -> GRAPHS Context
      Main COMPONENT -> COMPONENT Context
      Context COMPONENTCHANGED -> ITEM StoreComponent
      Context COMPONENTCHANGED -> COMPONENTS Main
      Context GRAPHCHANGED -> IN Check(ui/IgnoreExamples) OUT -> ITEM StoreGraph
      Context PROJECTCHANGED -> ITEM StoreProject
      Context RUNTIMECHANGED -> ITEM StoreRuntime
      Context NEWGRAPH -> ITEM StoreGraph
      Context NEWCOMPONENT -> ITEM StoreComponent

      # Handle GitHub tokens
      '' -> TOKEN Main GITHUBTOKEN -> GITHUBTOKEN Context
      Main DOWNLOAD -> IN Show(core/Output)
      Context UPLOAD -> IN Upload(local/Upload)

      # Example handling
      Router EXAMPLE -> GIST LoadExample(local/LoadExample)
      LoadExample GRAPH -> GRAPHS Context

      # Show/hide main widget
      'true' -> DATA ShowMain(core/Kick) OUT -> OPEN Main
      'false' -> DATA HideMain(core/Kick) OUT -> OPEN Main
      Router MAIN -> IN ShowMain
      Router ROUTE -> IN HideMain

      # Routing
      LoadData COMPLETE -> START ListenHash(interaction/ListenHash)
      ListenHash INITIAL -> URL Router
      ListenHash CHANGE -> URL Router
    </script>
    <script src="app/noflo.js"></script>
  </body>
</html>
