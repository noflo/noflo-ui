<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">

    <title>NoFlo Development Environment</title>
    <link rel="shortcut icon" href="favicon.ico" />

    <script src="bower_components/polymer/polymer.min.js"></script>
    <script src="browser/noflo-noflo-indexeddb/vendor/IndexedDBShim.min.js"></script>
    <link rel="import" href="bower_components/the-graph/the-graph-editor/the-graph-editor.html">
    <noflo-polymer name="the-graph-editor" inports="graph width height" outports="changed nodeclick edgeclick"></noflo-polymer>
    <link rel="import" href="browser/noflo-noflo-polymer/noflo-polymer/noflo-polymer.html">
    <link rel="import" href="elements/noflo-context.html">
    <noflo-polymer name="noflo-context" inports="project graphs component node edge buttons search clear" outports="project graphs currentgraph component nodes edges button result"></noflo-polymer>
    <link rel="import" href="elements/noflo-main.html">
    <noflo-polymer name="noflo-main" inports="open projects sketches examples" outports="newsketch newproject"></noflo-polymer>
    <link rel="import" href="elements/noflo-runtime.html">
    <noflo-polymer name="noflo-runtime" inports="graphs" outports="runtime"></noflo-polymer>
    <link rel="import" href="elements/noflo-search.html">
    <noflo-polymer name="noflo-search" inports="buttons results project graph component" outports="menu search"></noflo-polymer>
    <link rel="import" href="elements/noflo-library.html">
    <noflo-polymer name="noflo-library" inports="buttons editor search" outports="button result"></noflo-polymer>
    <link rel="import" href="elements/noflo-account.html">
    <link rel="import" href="elements/noflo-project.html">
    <noflo-polymer name="noflo-project" inports="project graphs components" outports="newgraph newcomponent"></noflo-polymer>
    <link rel="import" href="elements/noflo-component-editor.html">
    <noflo-polymer name="noflo-component-editor" inports="component" outports="changed"></noflo-polymer>
    <script src="./browser/noflo-ui.js"></script>
    <link rel="stylesheet" href="bower_components/font-awesome/css/font-awesome.min.css">
    <link rel="stylesheet" href="./css/noflo-ui.css">
    <meta name="viewport" content="width=device-width, initial-scale=1.0, maximum-scale=1.0, minimum-scale=1.0, user-scalable=no" />
  </head>
  <body>
    <noflo-main></noflo-main>
    <noflo-context></noflo-context>
    <noflo-runtime></noflo-runtime>
    <noflo-search></noflo-search>
    <noflo-library></noflo-library>
    <noflo-project></noflo-project>
    <main id="workspace"></main>
    <script type="application/fbp" id="PrepareStorage">
      EXPORT=OPEN.NAME:NAME
      EXPORT=ERRORS.OUT:ERROR
      EXPORT=SPLITDB.OUT:DB
      Open(indexeddb/Open) ERROR -> IN Errors(core/Merge)
      '1' -> VERSION Open
      Open DB -> IN SplitDb(core/Split)

      # Database upgrade setup
      Open UPGRADE -> UPGRADE Upgrade(indexeddb/UpgradeRouter)

      # ## Version 1

      # Project storage
      'projects' -> NAME CreateProjects(indexeddb/CreateStore)
      'id' -> KEYPATH CreateProjects
      Upgrade VERSIONS -> DB CreateProjects
      CreateProjects ERROR -> IN Errors

      # Graph storage
      'graphs' -> NAME CreateGraphs(indexeddb/CreateStore)
      'id' -> KEYPATH CreateGraphs
      CreateProjects DB -> DB CreateGraphs
      CreateGraphs ERROR -> IN Errors
      'project' -> NAME GraphProject(indexeddb/CreateIndex)
      'project' -> KEYPATH GraphProject
      CreateGraphs STORE -> STORE GraphProject
      # Migrate old LocalStorage data
      GraphProject STORE -> GRAPHSTORE Migrate(ui/MigrateLocalStorage)
      GraphProject ERROR -> IN Errors

      # Component storage
      'components' -> NAME CreateComponents(indexeddb/CreateStore)
      'id' -> KEYPATH CreateComponents
      CreateGraphs DB -> DB CreateComponents
      CreateComponents ERROR -> IN Errors
      'project' -> NAME ComponentProject(indexeddb/CreateIndex)
      'project' -> KEYPATH ComponentProject
      CreateComponents STORE -> STORE ComponentProject
      ComponentProject ERROR -> IN Errors
    </script>
    <script type="application/fbp" id="LoadData">
      EXPORT=READ.DB:DB
      EXPORT=MERGESTART.IN:LOAD
      EXPORT=GETPROJECTS.ITEM:PROJECTS
      EXPORT=GETSKETCHES.ITEM:SKETCHES
      MergeStart(core/Merge) OUT -> IN SplitStart(core/Split)
      'readonly' -> DATA HoldMode(core/Kick) OUT -> MODE Read(indexeddb/BeginTransaction)
      SplitStart OUT -> IN HoldMode
      'projects,graphs' -> DATA HoldStores(core/Kick) OUT -> STORES Read
      SplitStart OUT -> IN HoldStores

      # Read projects list
      'projects' -> DATA HoldProjectStore(core/Kick) OUT -> NAME ProjectStore(indexeddb/GetStore)
      SplitStart OUT -> IN HoldProjectStore
      Read TRANSACTION -> TRANSACTION ProjectStore
      '' -> DATA HoldAllProjects(core/Kick) OUT -> ALL GetProjects(indexeddb/Query)
      SplitStart OUT -> IN HoldAllProjects
      ProjectStore STORE -> STORE GetProjects

      # Read graphs that don't belong to a project
      'graphs' -> DATA HoldGraphStore(core/Kick) OUT -> NAME GraphStore(indexeddb/GetStore)
      SplitStart OUT -> IN HoldGraphStore
      ProjectStore TRANSACTION -> TRANSACTION GraphStore
      '' -> DATA HoldNoProject(core/Kick) OUT -> VALUE Sketches(indexeddb/QueryOnly)
      SplitStart OUT -> IN HoldNoProject
      Sketches RANGE -> RANGE GetSketches(indexeddb/Query)
      'project' -> DATA HoldProjectIndex(core/Kick) OUT -> NAME ProjectIndex(indexeddb/GetIndex)
      SplitStart OUT -> IN HoldProjectIndex
      GraphStore STORE -> STORE ProjectIndex INDEX -> STORE GetSketches
    </script>
    <script type="application/fbp" id="LoadProjectData">
      EXPORT=HOLDDB.DATA:DB
      EXPORT=GETID.IN:PROJECT
      EXPORT=GETGRAPHS.ITEM:GRAPHS
      EXPORT=GETCOMPONENTS.ITEM:COMPONENTS
      'id' -> KEY GetId(GetObjectKey) OUT -> IN Disc(core/DisconnectAfterPacket) OUT -> IN SplitId(core/Split)
      GetId MISSED -> IN DropMissed(core/Drop)
      SplitId OUT -> VALUE InProject(indexeddb/QueryOnly)
      'readonly' -> DATA HoldMode(core/Kick) OUT -> MODE Read(indexeddb/BeginTransaction)
      SplitId OUT -> IN HoldDb(core/Kick) OUT -> DB Read
      SplitId OUT -> IN HoldMode
      'graphs,components' -> DATA HoldStores(core/Kick) OUT -> STORES Read
      SplitId OUT -> IN HoldStores

      # Read graphs belonging to a project
      'graphs' -> DATA HoldGraphStore(core/Kick) OUT -> NAME GraphStore(indexeddb/GetStore)
      SplitId OUT -> IN HoldGraphStore
      Read TRANSACTION -> TRANSACTION GraphStore
      InProject RANGE -> RANGE GetGraphs(indexeddb/Query)
      'project' -> DATA HoldGraphIndex(core/Kick) OUT -> NAME ProjectGraphIndex(indexeddb/GetIndex)
      SplitId OUT -> IN HoldGraphIndex
      GraphStore STORE -> STORE ProjectGraphIndex INDEX -> STORE GetGraphs

      # Read components belonging to a project
      'components' -> DATA HoldComponentStore(core/Kick) OUT -> NAME ComponentStore(indexeddb/GetStore)
      SplitId OUT -> IN HoldComponentStore
      GraphStore TRANSACTION -> TRANSACTION ComponentStore
      GetGraphs RANGE -> RANGE GetComponents(indexeddb/Query)
      'project' -> DATA HoldComponentIndex(core/Kick) OUT -> NAME ProjectComponentIndex(indexeddb/GetIndex)
      SplitId OUT -> IN HoldComponentIndex
      ComponentStore STORE -> STORE ProjectComponentIndex INDEX -> STORE GetComponents
    </script>
    <script type="application/fbp" id="LoadGraph">
      EXPORT=HOLDDB.DATA:DB
      EXPORT=SPLITID.IN:ID
      EXPORT=GET.ITEM:GRAPH
      SplitId(core/Split) OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readonly' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      SplitId OUT -> IN HoldMode
      'graphs' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      SplitId OUT -> IN HoldStore
      SplitStore OUT -> NAME GraphStore(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION GraphStore STORE -> STORE Get(indexeddb/Get)
      SplitId OUT -> KEY Get
    </script>
    <script type="application/fbp" id="StoreGraph">
      EXPORT=MERGE.IN:ITEM
      EXPORT=HOLDDB.DATA:DB
      EXPORT=PUTGRAPH.KEY:KEY
      Merge(core/Merge) OUT -> IN Disc(core/DisconnectAfterPacket) OUT -> IN SplitGraph(core/Split)
      SplitGraph OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      SplitGraph OUT -> IN HoldMode
      'graphs' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      SplitGraph OUT -> IN HoldStore
      SplitStore OUT -> NAME GraphStore(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION GraphStore STORE -> STORE PutGraph(indexeddb/Put)
      'project' -> KEY GetProject(objects/GetObjectKey)
      SplitGraph OUT -> IN GetProject OBJECT -> IN MergeStorable(core/Merge) OUT -> VALUE PutGraph
      GetProject OUT -> IN DropProject(core/Drop)
      '' -> DATA HoldProject(core/Kick) OUT -> VALUE SetProject(objects/SetPropertyValue)
      SplitGraph OUT -> IN HoldProject
      'project' -> PROPERTY SetProject
      GetProject MISSED -> IN SetProject OUT -> IN MergeStorable
    </script>
    <script type="application/fbp" id="LoadProject">
      EXPORT=HOLDDB.DATA:DB
      EXPORT=SPLITID.IN:ID
      EXPORT=GET.ITEM:PROJECT
      SplitId(core/Split) OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readonly' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      SplitId OUT -> IN HoldMode
      'projects' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      SplitId OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE Get(indexeddb/Get)
      SplitId OUT -> KEY Get
    </script>
    <script type="application/fbp" id="PrepareProject">
      EXPORT=GENERATEID.IN:IN
      EXPORT=STOREGRAPH.DB:DB
      EXPORT=SETMAIN.OUT:OUT
      GenerateId(local/GenerateId) OUT -> IN Split(core/Split)
      'id' -> KEY GetId(objects/GetObjectKey)
      'project' -> PROPERTY SetProject(objects/SetPropertyValue)
      GetId OUT -> VALUE SetProject
      Split OUT -> IN GetId
      'name' -> KEY GetName(objects/GetObjectKey)
      'name' -> PROPERTY SetName(objects/SetPropertyValue)
      GetName OUT -> VALUE SetName
      Split OUT -> IN GetName
      'type' -> KEY GetType(objects/GetObjectKey)
      'type' -> PROPERTY SetType(objects/SetPropertyValue)
      GetType OUT -> VALUE SetType
      Split OUT -> IN GetType
      Split OUT -> START InitializeGraph(objects/CreateObject)
      InitializeGraph OUT -> IN SetName OUT -> IN SetType OUT -> DETAILS ToGraph(ui/CreateGraph)
      'toJSON' -> METHOD Convert(objects/CallMethod)
      ToGraph OUT -> IN Convert OUT -> IN SetProject OUT -> IN GenGraphId(local/GenerateId)
      GenGraphId OUT -> ITEM StoreGraph(local/StoreGraph)
      'main' -> PROPERTY SetMain(objects/SetPropertyValue)
      StoreGraph KEY -> IN SplitGraphId(core/Split) OUT -> VALUE SetMain
      Split OUT -> DATA HoldProject(core/Kick) OUT -> IN SetMain
      SplitGraphId OUT -> IN HoldProject
    </script>
    <script type="application/fbp" id="StoreProject">
      EXPORT=DISC.IN:ITEM
      EXPORT=HOLDDB.DATA:DB
      EXPORT=PUTPROJECT.KEY:KEY
      Disc(core/DisconnectAfterPacket) OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'projects' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE PutProject(indexeddb/Put)
      Split OUT -> VALUE PutProject
    </script>
    <script type="application/fbp" id="LoadComponent">
      EXPORT=HOLDDB.DATA:DB
      EXPORT=SPLITID.IN:ID
      EXPORT=GET.ITEM:COMPONENT
      SplitId(core/Split) OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readonly' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      SplitId OUT -> IN HoldMode
      'components' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      SplitId OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE Get(indexeddb/Get)
      SplitId OUT -> KEY Get
    </script>
    <script type="application/fbp" id="StoreComponent">
      EXPORT=MERGE.IN:ITEM
      EXPORT=HOLDDB.DATA:DB
      EXPORT=PUT.KEY:KEY
      Merge(core/Merge) OUT -> IN Disc(core/DisconnectAfterPacket) OUT -> IN Split(core/Split)
      Split OUT -> IN HoldDb(core/Kick) OUT -> DB Read(indexeddb/BeginTransaction)
      'readwrite' -> DATA HoldMode(core/Kick) OUT -> MODE Read
      Split OUT -> IN HoldMode
      'components' -> DATA HoldStore(core/Kick) OUT -> IN SplitStore(core/Split) OUT -> STORES Read
      Split OUT -> IN HoldStore
      SplitStore OUT -> NAME Store(indexeddb/GetStore)
      Read TRANSACTION -> TRANSACTION Store STORE -> STORE Put(indexeddb/Put)
      Split OUT -> VALUE Put
    </script>
    <script type="application/fbp" id="GenerateId">
      EXPORT=SPLITITEM.IN:IN
      EXPORT=SETID.OUT:OUT
      'id' -> PROPERTY SetId(objects/SetPropertyValue)
      SplitItem(core/Split) OUT -> START Generate(ui/GenerateId)
      Generate OUT -> VALUE SetId
      SplitItem OUT -> IN SetId
    </script>
    <script type="application/fbp" id="ProjectContext">
      EXPORT=CONTEXT.PROJECT:PROJECT
      EXPORT=CONTEXT.GRAPHS:GRAPHS
      EXPORT=CONTEXT.COMPONENT:COMPONENT
      EXPORT=CONTEXT.CLEAR:CLEAR
      EXPORT=SPLITWORKSPACE.IN:WORKSPACE
      EXPORT=EDITGRAPH.CHANGED:GRAPHCHANGED
      EXPORT=EDITCOMPONENT.CHANGED:COMPONENTCHANGED
      EXPORT=PROJECT.NEWGRAPH:NEWGRAPH
      EXPORT=PROJECT.NEWCOMPONENT:NEWCOMPONENT
      EXPORT=LOADPROJECTDATA.DB:DB
      'noflo-context' -> SELECTOR Context(polymer/noflo-context)

      # Runtime handling
      'noflo-runtime' -> SELECTOR Runtime(polymer/noflo-runtime)
      Context GRAPHS -> GRAPHS Runtime

      # Search handling
      'noflo-search' -> SELECTOR Search(polymer/noflo-search)
      Context CURRENTGRAPH -> GRAPH Search
      Context COMPONENT -> COMPONENT Search
      Context PROJECT -> PROJECT Search

      # Library handling
      'noflo-library' -> SELECTOR Library(polymer/noflo-library)

      # Main menu
      Search MENU -> BUTTONS Context BUTTON -> BUTTONS Search
      Search MENU -> BUTTONS Library BUTTON -> BUTTONS Search

      # UI searching
      Search SEARCH -> SEARCH Library RESULT -> RESULTS Search
      Search SEARCH -> SEARCH Context RESULT -> RESULTS Search

      # Project menu
      'noflo-project' -> SELECTOR Project(polymer/noflo-project)
      Context PROJECT -> PROJECT Project
      Context PROJECT -> PROJECT LoadProjectData(local/LoadProjectData)
      LoadProjectData GRAPHS -> GRAPHS Project
      LoadProjectData COMPONENTS -> COMPONENTS Project

      # Prepare graph editor
      Context CURRENTGRAPH -> CURRENTGRAPH EditGraph(local/GraphEditor)
      SplitWorkspace(core/Split) OUT -> WORKSPACE EditGraph
      Runtime RUNTIME -> RUNTIME EditGraph
      EditGraph NODECLICK -> NODE Context
      EditGraph EDGECLICK -> EDGE Context
      EditGraph EDITOR -> EDITOR Library

      # Prepare component editor
      Context COMPONENT -> COMPONENT EditComponent(local/ComponentEditor)
      SplitWorkspace OUT -> WORKSPACE EditComponent
    </script>
    <script type="application/fbp" id="ComponentEditor">
      EXPORT=SPLITCOMPONENT.IN:COMPONENT
      EXPORT=CREATEEDITOR.CONTAINER:WORKSPACE
      EXPORT=EDITOR.CHANGED:CHANGED
      'noflo-component-editor' -> STRING Send(strings/SendString) OUT -> TAGNAME CreateEditor(dom/CreateElement)
      CreateEditor ELEMENT -> IN SplitEditor(core/Split) OUT -> ELEMENT Editor(polymer/noflo-component-editor)
      SplitComponent OUT -> STRING HoldComponent(strings/SendString) OUT -> COMPONENT Editor
      SplitComponent(core/Split) OUT -> IN Send
      SplitEditor OUT -> IN HoldComponent
    </script>
    <script type="application/fbp" id="GraphEditor">
      EXPORT=LOADEDITOR.CONTAINER:WORKSPACE
      EXPORT=CONNECTRUNTIME.RUNTIME:RUNTIME
      EXPORT=MERGEGRAPH.IN:CURRENTGRAPH
      EXPORT=SPLITGRAPH.OUT:CURRENTGRAPH
      EXPORT=LOADEDITOR.EDITOR:EDITOR
      EXPORT=GRAPHEDITOR.NODECLICK:NODECLICK
      EXPORT=GRAPHEDITOR.EDGECLICK:EDGECLICK
      EXPORT=CONVERT.OUT:CHANGED
      MergeGraph(core/Merge) OUT -> IN SplitGraph(core/Split) OUT -> GRAPH LoadEditor(ui/LoadGraphEditor)
      LoadEditor EDITOR -> EDITOR ConnectRuntime(ui/ConnectRuntime)
      ConnectRuntime EDITOR -> ELEMENT GraphEditor(polymer/the-graph-editor)
      'toJSON' -> METHOD Convert(objects/CallMethod)
      GraphEditor CHANGED -> IN Convert
    </script>

    <script type="application/fbp" id="ClearWorkspace">
      EXPORT=CLEARWORKSPACE.CONTAINER:WORKSPACE
      EXPORT=DOCLEARWORKSPACE.IN:CLEAR
      '' -> DATA DoClearWorkspace(core/Kick)
      DoClearWorkspace OUT -> HTML ClearWorkspace(dom/WriteHtml)
    </script>
    <script type="application/fbp" id="main">
      'noflo-ui' -> NAME Storage(local/PrepareStorage)

      # Prepare the main view
      'noflo-main' -> SELECTOR Main(polymer/noflo-main)
      Storage DB -> DB LoadData(local/LoadData)
      Storage ERROR -> IN ShowErrors(core/Output)
      '' -> LOAD LoadData
      LoadData PROJECTS -> PROJECTS Main
      LoadData SKETCHES -> SKETCHES Main
      Storage DB -> DB StoreNewGraph(local/StoreGraph)
      Main NEWSKETCH -> IN NewGraphId(local/GenerateId) OUT -> ITEM StoreNewGraph KEY -> LOAD LoadData
      Storage DB -> DB StoreNewProject(local/StoreProject)
      Storage DB -> DB PrepareProject(local/PrepareProject)
      Main NEWPROJECT -> IN PrepareProject OUT -> ITEM StoreNewProject KEY -> LOAD LoadData

      # Prepare workspace
      '#workspace' -> SELECTOR GetWorkspace(dom/GetElement) ELEMENT -> IN Workspace(core/Split)
      Workspace OUT -> WORKSPACE Context

      # Prepare the context
      Router(ui/Router) PROJECT -> ID LoadProject(local/LoadProject) PROJECT -> PROJECT Context(local/ProjectContext)
      Storage DB -> DB LoadProject
      Router ROUTE -> CLEAR Context
      Storage DB -> DB Context
      Storage DB -> DB LoadGraph(local/LoadGraph)
      Router GRAPH -> ID LoadGraph GRAPH -> GRAPHS Context
      Storage DB -> DB LoadComponent(local/LoadComponent)
      Router COMPONENT -> ID LoadComponent COMPONENT -> COMPONENT Context
      Storage DB -> DB StoreComponent(local/StoreComponent)
      Context COMPONENTCHANGED -> ITEM StoreComponent
      Storage DB -> DB StoreGraph(local/StoreGraph)
      Context GRAPHCHANGED -> ITEM StoreGraph
      Context NEWGRAPH -> IN NewProjectGraphId(local/GenerateId) OUT -> ITEM StoreGraph
      Context NEWCOMPONENT -> IN NewProjectComponentId(local/GenerateId) OUT -> ITEM StoreComponent

      # Show/hide main widget
      'true' -> DATA ShowMain(core/Kick) OUT -> OPEN Main
      'false' -> DATA HideMain(core/Kick) OUT -> OPEN Main
      Router MAIN -> IN ShowMain
      Router ROUTE -> IN HideMain

      # Clear workspace on route changes
      Workspace OUT -> WORKSPACE ClearWorkspace(local/ClearWorkspace)
      Router ROUTE -> CLEAR ClearWorkspace

      # Routing
      Storage DB -> START ListenHash(interaction/ListenHash)
      ListenHash INITIAL -> URL Router
      ListenHash CHANGE -> URL Router
    </script>
    <script src="app/noflo.js"></script>
  </body>
</html>
