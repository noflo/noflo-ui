INPORT=LoadData.DB:DB
INPORT=Dispatch.IN:IN
INPORT=FindRuntimes.CONTEXT:CONTEXT
OUTPORT=MergeContext.OUT:CONTEXT
OUTPORT=UpdateLibrary.RUNTIME:OUT

# Route requests
'open,connect,sendGraph,sendComponent,runTests' -> ROUTES Dispatch(routers/GroupRouter)
Dispatch ROUTE -> START Loading(ui/CreateLoadingContext)
Loading OUT -> IN MergeContext(core/Merge)
Dispatch MISSED -> IN ShowErrors(core/Output)

# Send all projects to the UI
'' -> START CreateRuntimesCtx(objects/CreateObject)
CreateRuntimesCtx OUT -> CONTEXT SetRuntimes(ui/SetToContext)
'runtimes' -> KEY SetRuntimes
LoadData(ui/LoadRuntimeData) RUNTIMES -> RUNTIMES EnsureLocal(ui/EnsureLocalRuntimes)
EnsureLocal RUNTIMES -> VALUE SetRuntimes CONTEXT -> IN MergeContext

# Add runtime connection to an existing context
Dispatch OUT[1] -> CONTEXT ProjectRuntime(ui/ProjectRuntime)

# Add compatible runtimes to already-populated contexts
EnsureLocal RUNTIMES -> RUNTIMES FindRuntimes(ui/FindRuntimes)
FindRuntimes CONTEXT -> CONTEXT ProjectRuntime(ui/ProjectRuntime)
ProjectRuntime CONTEXT -> IN MergeContextPreSubscribe(core/Merge)

# Build direct runtime context
EnsureLocal RUNTIMES -> RUNTIMES PopulateRuntimeData(ui/PopulateRuntimeData)
Dispatch OUT[0] -> IN PopulateRuntimeData
PopulateRuntimeData OUT -> CONTEXT DirectRuntime(ui/DirectRuntime)
DirectRuntime CONTEXT -> IN MergeContextPreSubscribe

# Get components list when connecting to new runtime
ProjectRuntime RUNTIME -> RUNTIME ListComponents(runtime/ListComponents)
DirectRuntime RUNTIME -> RUNTIME ListComponents

ProjectRuntime RUNTIME -> RUNTIME ListenRuntime(runtime/ListenRuntime)
DirectRuntime RUNTIME -> RUNTIME ListenRuntime

ListenRuntime CONNECTED -> IN Drop(core/Drop)

ListComponents OUT -> IN UpdateLibrary(ui/UpdateComponentLibrary)
ProjectRuntime RUNTIME -> RUNTIME UpdateLibrary
DirectRuntime RUNTIME -> RUNTIME UpdateLibrary
UpdateLibrary OUT -> IN MergeContext
ListComponents ERROR -> IN ShowErrors

# Graph change subscription
MergeContextPreSubscribe OUT -> CONTEXT SubscribeGraph(ui/SubscribeGraph)
SubscribeGraph RUNTIME -> RUNTIME SendGraph(runtime/SendGraphChanges)
SubscribeGraph GRAPH -> GRAPH SendGraph

SubscribeGraph GRAPH -> GRAPH UpdateGraph(ui/UpdateGraph)
ListenRuntime GRAPH -> EVENT UpdateGraph

SubscribeGraph OUT -> IN MergeContext(core/Merge)

SubscribeGraph RUNTIME -> RUNTIME ListenNetwork(runtime/ListenNetwork)
SubscribeGraph GRAPH -> GRAPH ListenNetwork
ListenNetwork PACKET -> PACKET PacketToContext(ui/PacketToContext)
PacketToContext OUT -> IN MergeContext

# Listen to runtime process error events
SubscribeGraph RUNTIME -> RUNTIME ListenProcessErrors(runtime/ListenProcessErrors)
ListenProcessErrors PROCESS -> PROCESS ProcessErrorToContext(ui/ProcessErrorToContext)
ListenProcessErrors MESSAGE -> MESSAGE ProcessErrorToContext
ProcessErrorToContext OUT -> IN MergeContext

# Sending newly-created graphs and components to runtime
Dispatch OUT[2] -> GRAPH SendNewGraph(runtime/SendGraph)
ProjectRuntime RUNTIME -> RUNTIME SendNewGraph
DirectRuntime RUNTIME -> RUNTIME SendNewGraph
Dispatch OUT[3] -> COMPONENT SendNewComponent(runtime/SendComponent)
ProjectRuntime RUNTIME -> RUNTIME SendNewComponent
DirectRuntime RUNTIME -> RUNTIME SendNewComponent

# Running fbp-spec tests on the runtime
Dispatch OUT[4] -> IN RunTests(ui/RunTests)
ProjectRuntime RUNTIME -> RUNTIME RunTests
DirectRuntime RUNTIME -> RUNTIME RunTests
RunTests CONTEXT -> IN MergeContext
RunTests ERROR -> IN ShowErrors

LoadData ERROR -> IN ShowErrors
