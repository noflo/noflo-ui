# @runtime noflo-browser
# @icon address-card-o
INPORT=Dispatch.IN:IN
OUTPORT=Passed.OUT:PASS
OUTPORT=NewActions.OUT:NEW

'flowhub:runtimes:fetch,flowhub:runtimes:register,flowhub:runtimes:remove,flowhub:projects:fetch,github:sync:pull,github:sync:prepare,flowhub:projects:remove' -> ROUTES Dispatch(ui/DispatchAction)
Dispatch PASS -> IN Passed(core/Repeat)

# New actions generated by this middleware
'storage:save:runtime' -> ACTION RuntimeSaveAction(ui/SetAction) OUT -> IN NewActions(core/Merge)
'storage:delete:runtime' -> ACTION RuntimeDeleteAction(ui/SetAction) OUT -> IN NewActions
'flowhub:projects' -> ACTION RegistryProjectsAction(ui/SetAction) OUT -> IN NewActions
'flowhub:error' -> ACTION RegistryErrorAction(ui/SetAction) OUT -> IN NewActions

# Fetching list of user's registered runtimes
'user,runtimes' -> KEYS GetUserStateFetch(ui/GetActionValues)
Dispatch HANDLE[0] -> IN GetUserStateFetch
GetUserStateFetch VALUES[0] -> USER GetRuntimes(ui/GetRemoteRuntimes)
GetUserStateFetch VALUES[1] -> RUNTIMES GetRuntimes
GetRuntimes RUNTIME -> IN RuntimeSaveAction
GetRuntimes ERROR -> IN RegistryErrorAction

# Storing a new runtime definition to registry
'user' -> KEYS GetUserStatePut(ui/GetActionValues)
Dispatch HANDLE[1] -> IN GetUserStatePut
GetUserStatePut VALUES[0] -> USER SaveRuntime(ui/StoreRemoteRuntime)
GetUserStatePut OUT -> IN SaveRuntime
SaveRuntime OUT -> IN RuntimeSaveAction
SaveRuntime ERROR -> IN RegistryErrorAction

# Removing a runtime definition from registry
'user' -> KEYS GetUserStateDel(ui/GetActionValues)
Dispatch HANDLE[2] -> IN GetUserStateDel
GetUserStateDel VALUES[0] -> USER RemoveRuntime(ui/RemoveRemoteRuntime)
GetUserStateDel OUT -> IN RemoveRuntime
RemoveRuntime OUT -> IN RuntimeDeleteAction
RemoveRuntime ERROR -> IN RegistryErrorAction

# Fetching projects list from registry
'user' -> KEYS GetUserStateProjects(ui/GetActionValues)
Dispatch HANDLE[3] -> IN GetUserStateProjects
GetUserStateProjects VALUES[0] -> USER GetRemoteProjects(ui/GetRemoteProjects)
GetRemoteProjects PROJECTS -> IN RegistryProjectsAction
GetRemoteProjects ERROR -> IN RegistryErrorAction

# Registering a remote GitHub repository for pulls
'user' -> KEYS GetUserStateRepos(ui/GetActionValues)
Dispatch HANDLE[4] -> IN SplitPullAction(core/Split) OUT -> IN GetUserStateRepos
SplitPullAction OUT -> DATA SendPullAction(core/Kick) OUT -> IN Passed
GetUserStateRepos VALUES[0] -> USER StoreRemoteProject(ui/StoreRemoteProject)
GetUserStateRepos OUT -> IN StoreRemoteProject
StoreRemoteProject OUT -> IN SendPullAction
StoreRemoteProject ERROR -> IN RegistryErrorAction

# Preparing a bidirectional GitHub sync
Dispatch HANDLE[5] -> IN StoreRemoteProject

# Removing a repository from registry
'user' -> KEYS GetUserStateDelRepo(ui/GetActionValues)
Dispatch HANDLE[6] -> IN GetUserStateDelRepo
GetUserStateDelRepo VALUES[0] -> USER RemoveRepository(ui/RemoveRemoteProject)
GetUserStateDelRepo OUT -> IN RemoveRepository
RemoveRepository ERROR -> IN RegistryErrorAction