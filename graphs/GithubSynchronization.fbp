INPORT=Token.IN:TOKEN
INPORT=GetRepo.IN:PREPARE
INPORT=DoSync.IN:SYNCHRONIZE
INPORT=GetPullRepo.IN:PULL
OUTPORT=MergeContext.OUT:CONTEXT
OUTPORT=UpdatedGraph.OUT:GRAPH
OUTPORT=UpdatedComponent.OUT:COMPONENT
OUTPORT=UpdatedSpec.OUT:SPEC
OUTPORT=Errors.OUT:ERROR

# Preparation for sync: check local and remote situation
'repo' -> KEY GetRepo(objects/GetObjectKey)
Token(core/Repeat) OUT -> TOKEN GetReference(github/GetReference)
GetRepo OUT -> REPOSITORY GetReference
GetReference REFERENCE -> REFERENCE CheckSyncState(ui/CheckSyncState)
'project' -> KEY GetProject(objects/GetObjectKey)
GetRepo OBJECT -> IN GetProject OUT -> PROJECT CheckSyncState
Token OUT -> TOKEN CheckSyncState
GetReference ERROR -> IN Errors(core/Merge)
CheckSyncState ERROR -> IN Errors

# Different synchronization states
CheckSyncState NOOP -> IN MergeOps(core/Merge)
CheckSyncState LOCAL -> IN MergeOps
CheckSyncState REMOTE -> IN MergeOps
CheckSyncState BOTH -> IN MergeOps
'syncOperation' -> KEY OpsToContext(ui/SetToContext)
MergeOps OUT -> VALUE OpsToContext
MergeOps OUT -> START CreateCtx(ui/CreateEmptyContext) OUT -> CONTEXT OpsToContext
OpsToContext CONTEXT -> IN MergeContext(core/Merge)

# # Actual synchronization operation(s)

# First pull changes in
DoSync(core/Split) OUT -> IN PreparePulls(ui/PreparePulls)
Token OUT -> TOKEN GetBlob(github/GetBlob)
PreparePulls OUT -> OPERATION BlobToEntry(ui/BlobToEntry)
PreparePulls REPOSITORY -> REPOSITORY GetBlob
PreparePulls SHA -> SHA GetBlob
GetBlob OUT -> BLOB BlobToEntry
BlobToEntry COMPONENT -> IN UpdatedComponent(core/Merge)
BlobToEntry GRAPH -> IN UpdatedGraph(core/Merge)
BlobToEntry SPEC -> IN UpdatedSpec(core/Merge)
BlobToEntry ERROR -> IN Errors
GetBlob ERROR -> IN Errors

# Then push local changes
DoSync OUT -> IN PreparePush(ui/PreparePush)
PreparePush OUT -> IN UpdateFromTree(ui/UpdateFromTree)
PreparePush BASETREE -> BASE CreateTree(github/CreateTree)
Token OUT -> TOKEN CreateTree
PreparePush TREE -> TREE CreateTree
PreparePush REPOSITORY -> REPOSITORY CreateTree
CreateTree OUT -> TREE UpdateFromTree
PreparePush REPOSITORY -> REPOSITORY UpdateFromTree
Token OUT -> TOKEN UpdateFromTree
'sha' -> KEY GetTreeSha(objects/GetObjectKey)
CreateTree OUT -> IN GetTreeSha
GetTreeSha OUT -> TREE CreateCommit(github/CreateCommit)
Token OUT -> TOKEN CreateCommit
PreparePush MESSAGE -> MESSAGE CreateCommit
PreparePush PARENTCOMMITS -> PARENTS CreateCommit
PreparePush REPOSITORY -> REPOSITORY CreateCommit

CreateCommit OUT -> COMMIT UpdateReference(github/UpdateReference)
PreparePush REF -> REFERENCE UpdateReference
Token OUT -> TOKEN UpdateReference
PreparePush REPOSITORY -> REPOSITORY UpdateReference

UpdateFromTree COMPONENT -> IN UpdatedComponent
UpdateFromTree GRAPH -> IN UpdatedGraph
UpdateFromTree SPEC -> IN UpdatedSpec
UpdateFromTree ERROR -> IN Errors
CreateTree ERROR -> IN Errors
CreateCommit ERROR -> IN Errors
UpdateReference ERROR -> IN Errors

# Pull-only sync, no decision needed
'repo' -> KEY GetPullRepo(objects/GetObjectKey)
Token(core/Repeat) OUT -> TOKEN GetPullReference(github/GetReference)
GetPullRepo OUT -> REPOSITORY GetPullReference
GetPullReference REFERENCE -> REFERENCE CheckPullState(ui/CheckSyncState)
'project' -> KEY GetPullProject(objects/GetObjectKey)
GetPullRepo OBJECT -> IN GetPullProject OUT -> PROJECT CheckPullState
Token OUT -> TOKEN CheckPullState
GetPullReference ERROR -> IN Errors(core/Merge)
CheckPullState REMOTE -> IN DoSync
CheckPullState ERROR -> IN Errors
