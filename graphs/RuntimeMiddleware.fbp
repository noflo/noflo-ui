# @runtime noflo-browser
# @icon cogs
INPORT=Dispatch.IN:IN
OUTPORT=Passed.OUT:PASS
OUTPORT=NewActions.OUT:NEW

'runtime:open,storage:opened,context:edges,project:fromruntime,storage:ready,storage:stored:runtime' -> ROUTES Dispatch(ui/DispatchAction)
Dispatch PASS -> IN Passed(core/Merge)

# New actions generated by this middleware
'storage:save:project' -> ACTION ProjectSaveAction(ui/SetAction) OUT -> IN NewActions(core/Merge)
'storage:save:graph' -> ACTION GraphSaveAction(ui/SetAction) OUT -> IN NewActions
'storage:save:component' -> ACTION ComponentSaveAction(ui/SetAction) OUT -> IN NewActions
'flowhub:runtimes:register' -> ACTION RuntimeRegisterAction(ui/SetAction) OUT -> IN NewActions
'storage:save:runtime' -> ACTION RuntimeSaveAction(ui/SetAction) OUT -> IN NewActions
'runtime:error' -> ACTION RuntimeErrorAction(ui/SetAction) OUT -> IN NewActions
'runtime:opened' -> ACTION RuntimeOpenedAction(ui/SetAction) OUT -> IN NewActions
'runtime:opening' -> ACTION RuntimeOpeningAction(ui/SetAction) OUT -> IN NewActions
'runtime:status' -> ACTION RuntimeStatusAction(ui/SetAction) OUT -> IN NewActions
'runtime:components' -> ACTION RuntimeComponentsAction(ui/SetAction) OUT -> IN NewActions
'runtime:component' -> ACTION RuntimeComponentAction(ui/SetAction) OUT -> IN NewActions
'storage:opened' -> ACTION StorageOpenedAction(ui/SetAction) OUT -> IN Passed
'context:edges' -> ACTION ContextEdgesAction(ui/SetAction) OUT -> IN Passed

# Open runtime in live mode
'runtimes' -> KEYS GetRuntimesState(ui/GetActionValues)
Dispatch HANDLE[0] -> IN GetRuntimesState
GetRuntimesState VALUES[0] -> RUNTIMES PopulateRuntimeData(ui/PopulateRuntimeData)
GetRuntimesState OUT -> IN PopulateRuntimeData
PopulateRuntimeData OUT -> IN[0] GetClient
PopulateRuntimeData OUT -> IN RuntimeOpeningAction
PopulateRuntimeData NEW -> UPDATED GetClient
PopulateRuntimeData NEW -> IN RuntimeSaveAction
PopulateRuntimeData ERROR -> IN RuntimeErrorAction
GetClient OUT[0] -> IN OpenLiveMode(ui/OpenLiveMode)
GetClient CLIENT[0] -> CLIENT OpenLiveMode
OpenLiveMode OUT -> IN RuntimeOpenedAction
OpenLiveMode ERROR -> IN RuntimeErrorAction

# Connect runtime associated with project, if any
'runtimes' -> KEYS GetProjectRuntimesState(ui/GetActionValues)
Dispatch HANDLE[1] -> IN GetProjectRuntimesState
GetProjectRuntimesState VALUES[0] -> RUNTIMES PopulateProjectRuntime(ui/PopulateProjectRuntime)
GetProjectRuntimesState OUT -> IN PopulateProjectRuntime
PopulateProjectRuntime OUT -> IN[1] GetClient
PopulateProjectRuntime SKIPPED -> IN StorageOpenedAction
GetClient OUT[1] -> IN OpenProjectMode(ui/OpenProjectMode)
GetClient CLIENT[1] -> CLIENT OpenProjectMode
OpenProjectMode OUT -> IN StorageOpenedAction
OpenProjectMode ERROR -> IN RuntimeErrorAction

# Send selected edges to runtime
'graphs' -> KEYS GetEdgeState(ui/GetActionValues)
Dispatch HANDLE[2] -> IN GetEdgeState
GetEdgeState STATE -> IN[2] GetClient
GetClient CLIENT[2] -> CLIENT SendEdges(ui/SendEdges)
GetEdgeState VALUES[0] -> GRAPHS SendEdges
GetEdgeState OUT -> EDGES SendEdges
SendEdges OUT -> IN ContextEdgesAction
SendEdges ERROR -> IN RuntimeErrorAction

# Create a project based on data on runtime side
Dispatch HANDLE[3] -> IN RuntimeToProject(ui/RuntimeToProject)
RuntimeToProject PROJECT -> IN ProjectSaveAction
RuntimeToProject GRAPH -> IN GraphSaveAction
RuntimeToProject COMPONENT -> IN ComponentSaveAction
RuntimeToProject RUNTIME -> IN RuntimeRegisterAction
RuntimeToProject ERROR -> IN RuntimeErrorAction

# Populate local runtimes
'runtimes' -> KEYS GetStoredRuntimes(ui/GetActionValues)
Dispatch HANDLE[4] -> IN GetStoredRuntimes
GetStoredRuntimes VALUES[0] -> IN EnsureLocalRuntimes(ui/EnsureLocalRuntimes)
EnsureLocalRuntimes OUT -> IN RuntimeSaveAction
EnsureLocalRuntimes RUNTIMES -> INITIAL GetClient(ui/GetRuntimeClient)
GetClient ERROR -> IN RuntimeErrorAction

# Listen for runtime changes
GetClient INSTANCE -> IN ListenRuntime(ui/ListenRuntime)
ListenRuntime STATUS -> IN RuntimeStatusAction
ListenRuntime COMPONENTS -> IN RuntimeComponentsAction
ListenRuntime COMPONENT -> IN RuntimeComponentAction
ListenRuntime ERROR -> IN RuntimeErrorAction

# Update fbp-clients based on new runtime information
Dispatch HANDLE[5] -> UPDATED GetClient
GetClient UPDATED -> IN Passed
